apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: "jp.leafytree.android-scala"
apply plugin: 'com.jfrog.bintray'

android {
    buildToolsVersion androidBuildToolsVersion
    compileSdkVersion androidCompileSdkVersion

    defaultConfig {
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidCompileSdkVersion
        versionCode projVerAndroidCode
        versionName version
        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility androidSourceCompatibility
        targetCompatibility androidTargetCompatibility
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile "org.scala-lang:scala-library:$scalaVersion"
    compile 'com.android.support:multidex:1.0.1'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    from android.sourceSets.main.scala.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
//    archives javadocJar
    archives sourcesJar
}

install {
    repositories.mavenDeployer {
        repository(url: temporaryDir.toURI())
        pom {
            project {
                url siteUrl
                licenses {
                    license {
                        name licenseName
                        url licenseUrl
                    }
                }
                developers {
                    developer {
                        id developerMavenId
                        name developerName
                        email developerEmail
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    dryRun = false
    publish = false
    pkg {
        repo = bintrayRepoType
        name = bintrayRepoName
        userOrg = user
        licenses = [licenseBintrayCode]
        websiteUrl = siteUrl
        issueTrackerUrl = issuesUrl
        vcsUrl = gitUrl
        version {
            name = project.version
            released = new Date()
            vcsTag = projVerVcsTag
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = bintrayGPGPassphrase //The passphrase for GPG signing'
            }
        }
    }
}
